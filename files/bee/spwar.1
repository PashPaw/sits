1  CHAIN "$SPCWAR",(2-(TIM(1)<15))
50  ENTER #C6
51  IF C6=27 OR C6=28 THEN 59
54  CHAIN "$SCWR",55
55  IF TIM(1)<15 THEN 59
56  V9=0
59  V9=V9
60  GOSUB 4800
70  DIM Q$[5]
80  DIM G[8,8],Q[8,8],D[6],K[9,3],N[3]
90  DIM C$[20]
100  Q$=".EKB*"
110  T0=INT(RND(1)*20+20)*100
120  T=T0
130  T9=40
140  E0=3000
150  E=3000
160  P0=10
170  P=10
180  S9=200
190  DEF FND(D)=SQR((K[I,1]-S1)^2+(K[I,2]-S2)^2)
200  Q1=INT(RND(1)*8+1)
210  Q2=INT(RND(1)*8+1)
220  MAT D=ZER
230  S1=INT(RND(1)*8+1)
240  S2=INT(RND(1)*8+1)
250  DIM D$[72]
260  D$="WARP ENGINESS.R. SENSORSL.R. SENSORSPHASER CNTRLPHOTON TUBESDAMAGE CNTRL"
270  REM: SET UP GALAXY
280  B9=0
290  K9=0
300  FOR I=1 TO 8
310  FOR J=1 TO 8
320  R=RND(0)*64
330  K3=(R<.0001)+(R<.01)+(R<.03)+(R<.08)+(R<.28)+(R<1.28)
340  K3=K3+(R<3.28)+(R<6.28)+(R<13.28)
350  K9=K9+K3
360  B3=RND(0)>.96
370  B9=B9+B3
380  S3=INT(RND(1)*8+1)
390  G[I,J]=K3*100+B3*10+S3
400  NEXT J
410  NEXT I
420  K0=K9
430  IF B9>0 THEN 480
440  I=RND(1)*8+.5
450  J=RND(0)*8+.5
460  G[I,J]=G[I,J]+10
470  B9=1
480  GOTO 3620
490  REM: SET UP QUADRANT
500  K3=B3=S3=0
510  IF Q1<1 THEN 600
520  IF Q1>8 THEN 600
530  IF Q2<1 THEN 600
540  IF Q2>8 THEN 600
550  X=G[Q1,Q2]/100
560  K3=INT(X)
570  B3=INT((X-K3)*10)
580  S3=G[Q1,Q2]-INT(G[Q1,Q2]*.1)*10
590  MAT K=ZER
600  FOR I=1 TO 8
610  FOR J=1 TO 8
620  Q[I,J]=0
630  NEXT J
640  NEXT I
650  Q[S1,S2]=1
660  FOR I=1 TO K3
670  GOSUB 3570
680  Q[R1,R2]=2
690  K[I,1]=R1
700  K[I,2]=R2
710  K[I,3]=S9
720  NEXT I
730  FOR I=1 TO B3
740  GOSUB 3570
750  Q[R1,R2]=3
760  NEXT I
770  FOR I=1 TO S3
780  GOSUB 3570
790  Q[R1,R2]=4
800  NEXT I
810  A=-1
820  REM: SHORT RANGE SENSOR SCAN
830  FOR I=S1-1 MAX 1 TO S1+1 MIN 8
840  FOR J=S2-1 MAX 1 TO S2+1 MIN 8
850  IF Q[I,J] <> 3 THEN 900
860  C$="DOCKED"
870  E=3000
880  P=10
890  GOTO 990
900  NEXT J
910  NEXT I
920  IF K3>0 THEN 960
930  IF E<E0*.1 THEN 980
940  C$="GREEN"
950  GOTO 990
960  C$="RED"
970  GOTO 990
980  C$="YELLOW"
990  GOSUB A<0 OF 3260
1000  IF E <= 0 THEN 3380
1010  IF D[2] >= 0 THEN 1060
1020  PRINT 
1030  PRINT "***SHORT RANGE SENSORS ARE OUT ***"
1040  PRINT 
1050  GOTO 1310
1060  PRINT "----------------------"
1070  FOR I=1 TO 8
1080  FOR J=1 TO 8
1090  PRINT Q$[Q[I,J]+1,Q[I,J]+1];" ";
1100  NEXT J
1110  PRINT "   ";
1120  GOTO I OF 1170,1190,1210,1230,1250,1270,1290
1130  PRINT 
1140  NEXT I
1150  PRINT "--------------------------"
1160  GOTO 1310
1170  PRINT "STARDATE         "T
1180  GOTO 1140
1190  PRINT "CONDITION        "C$
1200  GOTO 1140
1210  PRINT "QUADRANT         "Q1;"-"Q2
1215  V9=V9
1220  GOTO 1140
1230  PRINT "SECTOR           "S1;"-"S2
1240  GOTO 1140
1250  PRINT "ENERGY           "E
1260  GOTO 1140
1270  PRINT "PHOTON TORPEDOES "P
1280  GOTO 1140
1290  PRINT "KLINGONS LEFT    "K9
1300  GOTO 1140
1310  PRINT "COMMAND:";
1320  INPUT A
1330  GOTO A+1 OF 1430,820,2120,2320,2560,3060
1340  PRINT 
1350  PRINT "   0 = SET COURSE"
1360  PRINT "   1 = SHORT RANGE SENSOR SCAN"
1370  PRINT "   2 = LONG RANGE SENSOR SCAN"
1380  PRINT "   3 = FIRE PHASERS"
1390  PRINT "   4 = FIRE PHOTON TORPEDOES"
1400  PRINT "   5 = DAMAGE CONTROL REPORT"
1410  PRINT 
1420  GOTO 1310
1430  REM: WARP DRIVE
1440  PRINT "COURSE (1-8.9999):";
1450  INPUT C1
1460  IF C1=0 THEN 1310
1470  IF C1<1 THEN 1440
1480  IF C1 >= 9 THEN 1440
1490  PRINT "WARP FACTOR (0-12):";
1500  INPUT W1
1510  IF W1<0 THEN 1440
1520  IF W1>12 THEN 1440
1530  IF W1 <= .2 THEN 1570
1540  IF D[1] >= 0 THEN 1570
1550  PRINT "WARP ENGINES ARE DAMAGED, MAXIMUM SPEED = WARP .2"
1560  GOTO 1440
1570  IF K3 <= 0 THEN 1600
1580  GOSUB 3260
1590  IF E <= 0 THEN 3410
1600  FOR I=1 TO 6
1610  D[I]=D[I]+1 MIN 0
1620  NEXT I
1630  IF RND(1)>.25 THEN 1850
1640  R1=INT(RND(1)*6+1)
1650  IF RND(1)>.5 THEN 1720
1660  D[R1]=D[R1]-(RND(1)*5+1)
1670  PRINT "*** SPACE STORM, ";
1680  S=(R1-1)*12+1
1690  PRINT D$[S,S+11]" DAMAGED ***"
1700  LET S=0
1710  GOTO 1850
1720  FOR I=R1 TO 6
1730  IF D[I]<0 THEN 1790
1740  NEXT I
1750  FOR I=1 TO R1-1
1760  IF D[I]<0 THEN 1790
1770  NEXT I
1780  GOTO 1840
1790  R1=I
1800  D[I]=D[I]+RND(1)*5+1 MIN 0
1810  PRINT "*** TRUCE, ";
1820  S=(R1-1)*12+1
1830  PRINT D$[S,S+11]" STATE OF REPAIR IMPROVED ***"
1840  LET S=0
1850  N=INT(W1*8)
1860  E=E-N-N+5
1870  T=T+1
1880  Q[S1,S2]=0
1890  X=S1
1900  Y=S2
1910  IF T>T0+T9 THEN 3380
1920  GOSUB 3030
1930  FOR I=1 TO N
1940  S1=S1+X1
1950  S2=S2+X2
1960  IF S1<.5 OR S2<.5 OR S1 >= 8.5 OR S2 >= 8.5 THEN 2070
1970  IF Q[S1,S2]=0 THEN 2020
1980  PRINT "ENTERPRISE BLOCKED BY OBJECT AT SECTOR"S1;"-"S2
1990  S1=S1-X1
2000  S2=S2-X2
2010  GOTO 2030
2020  NEXT I
2030  Q[S1,S2]=1
2040  S1=INT(S1+.5)
2050  S2=INT(S2+.5)
2060  GOTO 820
2070  Q1=INT(Q1+W1*X1+(X-.5)/8) MIN 8 MAX 1
2080  Q2=INT(Q2+W1*X2+(Y-.5)/8) MIN 8 MAX 1
2090  S1=INT(RND(0)*8+1)
2100  S2=INT(RND(0)*8+1)
2110  GOTO 490
2120  REM: LONG RANGE SENSOR SCAN
2130  IF D[3] >= 0 THEN 2160
2140  PRINT "LONG RANGE SENSORS ARE INOPERABLE"
2150  GOTO 1310
2160  PRINT "LONG RANGE SENSOR SCAN FOR QUADRANT"Q1","Q2
2170  PRINT "----------------- #"
2180  PRINT 
2190  FOR I=Q1-1 TO Q1+1
2200  N[1]=N[2]=N[3]=0
2210  FOR J=Q2-1 TO Q2+1
2220  IF I<1 THEN 2270
2230  IF I>8 THEN 2270
2240  IF J<1 THEN 2270
2250  IF J>8 THEN 2270
2260  N[J-Q2+2]=G[I,J]
2270  NEXT J
2280  PRINT ":"N[1]":"N[2]":"N[3]
2290  PRINT "------------------- #"
2300  NEXT I
2310  GOTO 1310
2320  REM: PHASER CONTROL
2330  IF D[4] >= 0 THEN 2360
2340  PRINT "PHASER CONTROL IS DISABLED"
2350  GOTO 1310
2360  PRINT "PHASERS LOCKED ON TARGET.  ENERGY AVAILABLE = "E
2370  PRINT "NUMBER OF UNITS TO FIRE:";
2380  INPUT X
2390  IF X <= 0 THEN 1310
2400  IF E-X<0 THEN 2360
2410  E=E-X
2420  IF K3=0 THEN 2530
2430  FOR I=1 TO 9
2440  IF K[I,3] <= 0 THEN 2520
2450  H=X/FND(0)*(2+RND(0))
2460  K[I,3]=K[I,3]-H
2470  PRINT H;"UNIT HIT ON KLINGON AT SECTOR"K[I,1];"-"K[I,2]
2480  PRINT "("K[I,3]"LEFT)"
2490  IF K[I,3]>0 THEN 2520
2500  GOSUB 3200
2510  IF K9 <= 0 THEN 3480
2520  NEXT I
2530  GOSUB 3260
2540  IF E <= 0 THEN 3410
2550  GOTO 1310
2560  REM: PHOTON TORPEDOES
2570  IF D[5] >= 0 THEN 2600
2580  PRINT "PHOTON TUBES ARE NOT OPERATIONAL"
2590  GOTO 1310
2600  IF P>0 THEN 2630
2610  PRINT "ALL PHOTON TORPEDOES EXPENDED"
2620  GOTO 1310
2630  PRINT "TORPEDO COURSE (1-8.9999):)";
2640  INPUT C1
2650  IF C1=0 THEN 1310
2660  IF C1<1 THEN 2630
2670  IF C1 >= 9 THEN 2630
2680  GOSUB 3030
2690  X=S1
2700  Y=S2
2710  P=P-1
2720  PRINT "TORPEDO TRACK:";
2730  X=X+X1
2740  Y=Y+X2
2750  IF X<.5 OR Y<.5 OR X >= 8.5 OR Y >= 8.5 THEN 2990
2760  PRINT X;"-"Y;
2770  IF Q[X,Y] <> 0 THEN 2790
2780  GOTO 2730
2790  IF Q[X,Y] <> 2 THEN 2890
2800  PRINT "*** KLINGON DESTROYED ***"
2810  K3=K3-1
2820  K9=K9-1
2830  IF K9 <= 0 THEN 3480
2840  FOR I=1 TO 9
2850  IF INT(X+.5)=K[I,1] AND INT(Y+.5)=K[I,2] THEN 2870
2860  NEXT I
2870  K[I,3]=0
2880  GOTO 2960
2890  IF Q[X,Y] <> 4 THEN 2930
2900  PRINT "STAR DESTROYED"
2910  S3=S3-1
2920  GOTO 2960
2930  PRINT 
2940  PRINT "*** STAR BASE DESTROYED ... CONGRATULATIONS ***"
2950  B3=B3-1
2960  Q[X,Y]=0
2970  G[Q1,Q2]=K3*100+B3*10+S3
2980  GOTO 3000
2990  PRINT "TORPEDO MISSED"
3000  GOSUB 3260
3010  IF E <= 0 THEN 3410
3020  GOTO 1310
3030  X2=COS((C1-1)*.785398)
3040  X1=-SIN((C1-1)*.785398)
3050  RETURN 
3060  REM: DAMAGE CONTROL REPORT
3070  IF D[6] >= 0 THEN 3100
3080  PRINT "DAMAGE CONTROL REPORT IS NOT AVAILABLE"
3090  GOTO 1310
3100  PRINT 
3110  Z=1
3120  PRINT "DEVICE         STATE OF REPAIR"
3130  FOR I=1 TO 6
3140  PRINT D$[Z,Z+11],D[I]
3150  LET Z=Z+12
3160  NEXT I
3170  PRINT 
3180  GOTO 1310
3190  REM:
3200  PRINT "KLINGON AT SECTOR"K[I,1];"-"K[I,2];"DESTROYED!"
3210  K3=K3-1
3220  K9=K9-1
3230  Q[K[I,1],K[I,2]]=0
3240  G[Q1,Q2]=K3*100+B3*10+S3
3250  RETURN 
3260  REM: KLINGON ATTACK
3270  IF C$ <> "DOCKED" THEN 3300
3280  PRINT "STAR BASE SHIELDS PROTECT THE ENTERPRISE"
3290  RETURN 
3300  FOR I=1 TO 9
3310  IF K[I,3] <= 0 THEN 3360
3320  H=(K[I,3]/FND(0))*(2+RND(1))
3330  E=E-H
3340  PRINT H;"UNIT HIT ON ENTERPRISE FROM KLINGON AT SECTOR"K[I,1];"-"K[I,2]
3350  PRINT "("E;"LEFT)"
3360  NEXT I
3370  RETURN 
3380  REM: LOSE
3390  GOSUB 4810
3400  PRINT "IT IS STARDATE"T
3410  GOTO 3430
3420  GOSUB 4810
3430  PRINT "THE ENTERPRISE HAS BEEN DESTROYED   "
3440  PRINT "THE FEDERATION WILL BE CONQUERED."
3450  PRINT "THERE ARE STILL"K9;"KLINGON BATTLE CRUISERS."
3460  PRINT "YOU ARE DEAD."
3470  GOTO 60
3480  REM: WIN
3490  GOSUB 4810
3500  PRINT "IT IS STARDATE"T
3510  PRINT "THE LAST KLINGON BATTLE CRUISER IN THE GALAXY HAS BEEN"
3520  PRINT "DESTROYED."
3530  PRINT "THE FEDERATION HAS BEEN SAVED"
3540  PRINT "YOU HAVE BEEN PROMOTED TO ADMIRAL."
3550  PRINT K0;"KLINGONS IN"T-T0;"YEARS.  RATING ="INT(K0/(T-T0)*1000)
3560  GOTO 60
3570  REM: RANDOM NUMBER GENERATOR
3580  R1=INT(RND(1)*8+1)
3590  R2=INT(RND(1)*8+1)
3600  IF Q[R1,R2] <> 0 THEN 3580
3610  RETURN 
3620  PRINT "ORDERS:  STARDATE = "T
3630  PRINT 
3640  PRINT "   AS COMMANDER OF THE UNITED SPACESHIP ENTERPRISE,"
3650  PRINT "YOUR MISSION IS TO RID THE GALAXY OF THE DEADLY"
3660  PRINT "KLINGON MENACE.  TO DO THIS, YOU MUST DESTROY THE "
3670  PRINT "KLINGON INVASION FORCE OF "K9"BATTLE CRUISERS."
3680  PRINT "YOU HAVE"T9;"SOLAR YEARS TO COMPLETE YOUR MISSION."
3690  PRINT "(I.E. UNTIL STARDATE"T0+T9;")."
3700  PRINT 
3710  PRINT "   YOU HAVE AT LEAST ONE SUPPORTING STARBASE."
3720  PRINT "WHEN THE ENTERPRISE DOCKS AT ONE (IS POSITIONED"
3730  PRINT "NEXT TO ONE) IT IS RESUPPLIED WITH ENERGY AND"
3740  PRINT "PHOTON TORPEDOES."
3750  PRINT "THE ENTERPRISE IS CURRENTLY IN QUADRANT"Q1"-"Q2"SECTOR"S1"-"S2
3760  PRINT "DO YOU NEED FURTHER INSTRUCTIONS";
3770  INPUT A$[1,1]
3780  IF A$="N" THEN 490
3790  PRINT "   THE GALAXY IS DIVIDED INTO 64 QUADRANTS WITH"
3800  PRINT "THE FOLLOWING COORDINATES:"
3810  PRINT " #   1   2   3   4   5   6   7   8   #"
3820  PRINT "   --------------------------------- #"
3830  FOR I=1 TO 8
3840  PRINT " # :   :   :   :   :   :   :   :   :  "I
3850  PRINT "   --------------------------------- #"
3860  NEXT I
3870  PRINT 
3880  PRINT 
3890  PRINT 
3900  PRINT "EACH QUADRANT IS SIMILARLY DIVIDED INTO 64 SECTORS."
3910  GOSUB 4770
3920  PRINT "::: DEVICES :::"
3930  PRINT 
3940  PRINT "WARP ENGINES: "
3950  PRINT 
3960  PRINT "COURSE = A REAL NUMBER FROM 1 TO 8.99999."
3970  PRINT "NUMBERS INDICATE DIRECTION STARTING AT THE RIGHT AND"
3980  PRINT "GOING COUNTER CLOCKWISE:"
3990  PRINT 
4000  PRINT "        3"
4010  PRINT "      4   2"
4020  PRINT "         /"
4030  PRINT "    5---*---1"
4040  PRINT "       /"
4050  PRINT "      6   8"
4060  PRINT "        7"
4070  PRINT 
4080  PRINT "WARP FACTOR = A REAL NUMBER FROM 0 TO 8."
4090  PRINT "DISTANCE TRAVELED = INTEGER (WARP FACTOR) QUADRANTS:"
4100  PRINT "   WARP .2 = THE ENTERPRISE TRAVELS 1 SECTOR."
4110  PRINT "        .5 =                        4 SECTORS."
4120  PRINT "         1 =                        1 QUADRANT."
4130  PRINT "         2 =                        2 QUADRANTS."
4140  GOSUB 4770
4150  PRINT "FOR EXAMPLE, IF YOU TRAVEL FROM:"
4160  PRINT "QUADRANT 1 - 1, SECTOR 1 - 1, IN DIRECTION 1 AT WARP 2"
4170  PRINT "YOU WOULD STOP AT"
4180  PRINT "QUADRANT 1 - 3, SECTOR 1 - 1 IN THE NEXT STARDATE."
4190  PRINT 
4200  PRINT "*** NOTE:  EVERY USE OF THE WARP ENGINES TAKES ONE"
4210  PRINT "STARDATE.  IF THE ENTERPRISE IS BLOCKED BY SOMETHING"
4220  PRINT "DURING INTRA-QUADRANT TRAVEL IT WILL STOP IN FRONT"
4230  PRINT "OF IT (AND WASTE A STARDATE) ***"
4240  GOSUB 4770
4250  PRINT "SHORT RANGE SENSORS:"
4260  PRINT 
4270  PRINT "   THE SHORT RANGE SENSORS OF THE ENTERPRISE DISPLAY"
4280  PRINT "A DETAILED VIEW OF THE QUADRANT IT IS CURRENTLY IN."
4290  PRINT "THE ENTERPRISE LOOKS LIKE 'E' ON THE SCREEN."
4300  PRINT "KLINGON BATTLE CRUISERS LOOK LIKE 'K' ON THE SCREEN."
4310  PRINT "STARBASES LOOK LIKE 'B' AND STARS LOOK LIKE * ."
4320  PRINT 
4330  PRINT 
4340  PRINT "LONG RANGE SENSORS:"
4350  PRINT 
4360  PRINT "   THE LONG RANGE SENSORS OF THE ENTERPRISE DISPLAY"
4370  PRINT "THE NUMBER OF OBJECTS IN THE 9 CLOSEST QUADRANTS."
4380  PRINT "EACH DIGIT OF THE NUMBER IN EACH BOX MEANS SOMETHING:"
4390  PRINT "   THE ONES DIGIT REPRESENTS THE NUMBER OF STARS."
4400  PRINT "       TENS                                STARBASES."
4410  PRINT "      HUNDREDS                             KLINGONS."
4420  PRINT "FOR EXAMPLE:"
4430  PRINT "319 MEANS  3 KLINGONS, 1 STARBASE, AND 9 STARS."
4440  PRINT "206 MEANS 2 KLINGONS, 0 STARBASES, AND 6 STARS."
4450  PRINT "7 MEANS 0 KLINGONS, 0 STARBASES, AND 7 STARS."
4460  GOSUB 4770
4470  PRINT "PHASERS:"
4480  PRINT 
4490  PRINT "   ANY PORTION OF THE ENERGY AVAILABLE CAN BE FIRED."
4500  PRINT "THE BATTLE COMPUTER DIVIDES THIS AMOUNT AMONG THE"
4510  PRINT "KLINGON CRUISERS IN THE QUADRANT AND DETERMINES"
4520  PRINT "THE VARIOUS DIRECTIONS OF FIRE.  THE EFFECTIVENESS"
4530  PRINT "OF A HIT DEPENDS MOSTLY ON THE DISTANCE TO THE"
4540  PRINT "TARGET.  A KLINGON BATTLE CRUISER STARTS WITH"S9
4550  PRINT "UNITS OF ENERGY.  IT CAN FIRE AN AMOUNT EQUAL TO"
4560  PRINT "WHATEVER ENERGY IT HAS LEFT."
4570  PRINT 
4580  PRINT 
4590  PRINT "PHOTON TORPEDOES:"
4600  PRINT 
4610  PRINT "   INITIALLY THE ENTERPRISE HAS"P0"PHOTON TORPEDOES."
4620  PRINT "ONE TORPEDO DESTROYS WHATEVER IT HITS.  THE RANGE"
4630  PRINT "OF PHOTON TORPEDOES (LIKE PHASERS) IS LIMITED TO"
4640  PRINT "THE CURRENT QUADRANT.  THE COURSE OF A PHOTON TORPEDO"
4650  PRINT "IS SET THE SAME WAY AS THAT OF THE ENTERPRISE."
4660  GOSUB 4770
4670  PRINT "DAMAGE CONTROL REPORT:"
4680  PRINT "   THE DAMAGE CONTROL REPORT LISTS THE MAIN DEVICES AND"
4690  PRINT "THEIR STATE OF REPAIR.  A NEGATIVE STATE OF REPAIR"
4700  PRINT "INDICATES A DISABLED DEVICE.  DEVICES CAN BE DAMAGED OR"
4710  PRINT "REPAIRED BY A SPACE STORM OR TRUCE, RESPECTIVELY."
4720  PRINT "ALSO, DAMAGED DEVICES ARE REPAIRED 1 UNIT EVERY STARDATE."
4730  PRINT 
4740  PRINT 
4750  GOSUB 4770
4760  GOTO 490
4770  PRINT 
4780  PRINT "END OF PAGE, INPUT ANY NUMBER";
4790  INPUT X
4800  REM
4810  FOR I=1 TO 3
4815  V9=V9
4820  PRINT 
4830  NEXT I
4840  RETURN 
4850  END 
